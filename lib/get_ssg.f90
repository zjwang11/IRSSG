module get_ssg_mod
  use lib_params, only:dp, lattice
  implicit none
  integer, parameter :: ssg_file_unit = 1245
  integer, parameter,public :: max_nsym = 2400
contains

  subroutine get_ssg_op(spg, ssg_label,nsym,rotation,tau,spin_SU2,spin_rotation,time_reversal)
  ! This subroutine reads space group symmetries from files generated by identifySSG
  ! and stores them in the provided arrays.
    implicit none
    integer,intent(out) :: rotation(3,3,max_nsym)
    real(dp),intent(out) :: tau(3,max_nsym)
    complex(dp),intent(out) :: spin_SU2(2,2,max_nsym)
    integer,intent(out) :: time_reversal(max_nsym)
    integer, intent(out) :: nsym
    real(dp), intent(out) :: spin_rotation(3,3,max_nsym)
    character(len=100), intent(out) :: ssg_label
    integer, intent(out) :: spg

    real(dp) :: spin_only_rotation(3,3,4)
    complex(dp) :: spin_only_SU2(2,2,4)
    integer :: spin_only_time_reversal(4)
    integer :: num_spin_only_operator

    ! local variables
    character(len=1024) :: ssg_info(4)
    character(len=1024) :: line
    integer :: idx, nlines, ios, i, start, j
    integer, parameter :: N = 4
    integer :: nsym_tmp

    integer :: rotation_tmp2(3,3,max_nsym)
    real(dp) :: tau_tmp2(3,max_nsym)
    complex(dp) :: spin_SU2_tmp2(2,2,max_nsym)
    integer :: time_reversal_tmp2(max_nsym)
    integer :: nsym_tmp2
    real(dp) :: spin_rotation_tmp2(3,3,max_nsym)
    integer :: iostat
    
    spin_only_rotation = 0.0_dp
    spin_only_SU2 = 0
    spin_only_time_reversal = 0

    spin_SU2 = cmplx(0.0_dp, 0.0_dp, kind=dp)
    tau = 0.0_dp
    rotation = 0
    spin_rotation = 0.0_dp
    time_reversal = 0

    spin_rotation_tmp2 = 0.0_dp
    spin_SU2_tmp2 = cmplx(0.0_dp, 0.0_dp, kind=dp)
    tau_tmp2 = 0.0_dp
    rotation_tmp2 = 0
    time_reversal_tmp2 = 0


    nsym = 0
    nsym_tmp2 = 0

    num_spin_only_operator = 0

    open(unit=ssg_file_unit, file='ssg.data', form='unformatted', access='stream', status='old', action='read', iostat=ios)
    read(ssg_file_unit) spg
    read(ssg_file_unit) nsym
    read(ssg_file_unit) ssg_label(1:nsym)

    read(ssg_file_unit) num_spin_only_operator
    read(ssg_file_unit) spin_only_rotation(:,:,1:num_spin_only_operator)
    read(ssg_file_unit) spin_only_SU2(:,:,1:num_spin_only_operator)
    read(ssg_file_unit) spin_only_time_reversal(1:num_spin_only_operator)

    do i=1,num_spin_only_operator
      spin_only_rotation(:,:,i) = transpose(spin_only_rotation(:,:,i))
      spin_only_SU2(:,:,i) = transpose(spin_only_SU2(:,:,i))
    enddo

    read(ssg_file_unit) nsym
    nsym_tmp2 = nsym
    read(ssg_file_unit) spin_rotation_tmp2(:,:,1:nsym_tmp2)

    read(ssg_file_unit) spin_SU2_tmp2(:,:,1:nsym_tmp2)
    read(ssg_file_unit) time_reversal_tmp2(1:nsym_tmp2)

    read(ssg_file_unit) rotation_tmp2(:,:,1:nsym_tmp2)

    read(ssg_file_unit) tau_tmp2(:,1:nsym_tmp2)

    do i=1,nsym_tmp2
      rotation_tmp2(:,:,i) = transpose(rotation_tmp2(:,:,i))
      spin_rotation_tmp2(:,:,i) = transpose(spin_rotation_tmp2(:,:,i))
      spin_SU2_tmp2(:,:,i) = transpose(spin_SU2_tmp2(:,:,i))
    enddo

    close(ssg_file_unit)

    
    do i=1,nsym
      do j=1,num_spin_only_operator
        time_reversal((i-1)*num_spin_only_operator+j) = spin_only_time_reversal(j) * time_reversal_tmp2(i)
        spin_SU2(:,:,(i-1)*num_spin_only_operator+j) = matmul(spin_only_SU2(:,:,j),spin_SU2_tmp2(:,:,i))
        spin_rotation(:,:,(i-1)*num_spin_only_operator+j) = matmul(spin_only_rotation(:,:,j),spin_rotation_tmp2(:,:,i))  
        tau(:,(i-1)*num_spin_only_operator+j) = tau_tmp2(:,i)
        rotation(:,:,(i-1)*num_spin_only_operator+j) = rotation_tmp2(:,:,i)
      enddo
    enddo
    
    nsym = nsym * num_spin_only_operator

    tau(:,1:nsym) = mod(tau(:,1:nsym)+0.5_dp,1.0_dp)-0.5_dp

#ifdef TEST
    do i=1,nsym
        write(104,*)time_reversal_tmp2(i)
        write(104,*)transpose(spin_SU2_tmp2(:,:,i))
    enddo
    do i=1,nsym
        write(105,*)transpose(rotation(:,:,i))
    enddo
    do i=1,nsym
        write(102,*)transpose(rotation_tmp2(:,:,i))
    enddo
    do i=1,nsym
        write(103,*)tau_tmp2(:,i)
    enddo
    do i=1,nsym
        write(109,*)transpose(spin_rotation_tmp2(:,:,i))
    enddo
#endif

  end subroutine get_ssg_op




  subroutine judge_up_down_relation(num_litt_group,spin_SU2,time_reversal,spin_no_reversal)
    integer, intent(in) :: num_litt_group
    complex(dp), intent(in) :: spin_SU2(2,2,num_litt_group)
    integer, intent(in) :: time_reversal(num_litt_group)
    logical, intent(out) :: spin_no_reversal

    integer :: i

    spin_no_reversal = .true.
    do i=1,num_litt_group
      if (time_reversal(i) == 1) then
        if (abs(spin_SU2(1,2,i)) > 1e-4 .or. abs(spin_SU2(2,1,i)) > 1e-4) then
          spin_no_reversal = .false.
          return
        endif
      else
        if (abs(spin_SU2(1,1,i)) > 1e-4 .or. abs(spin_SU2(1,1,i)) > 1e-4) then
          spin_no_reversal = .false.
          return
        endif
      endif
    enddo

  endsubroutine




  subroutine output_ssg_operator(ssg_label,nsym,rotation,tau,spin_SU2,spin_rotation,time_reversal)
    implicit none
    integer,intent(in) :: nsym
    integer,intent(in) :: rotation(3,3,nsym)
    real(dp),intent(in) :: tau(3,nsym)
    real(dp),intent(in) :: spin_rotation(3,3,nsym)
    integer,intent(in) :: time_reversal(nsym)
    complex(dp),intent(in) :: spin_SU2(2,2,nsym)
    integer :: i,irot
    real(dp) :: br2(3,3)
    real(dp) :: br4(3,3)
    real(dp) :: SO3(3,3,nsym)
    character(len=100), intent(in) :: ssg_label
    character(len=4) :: tmp

    br2 = lattice
    call invreal33(br2, br4) 

    SO3 = 0.0_dp
    do irot=1,nsym
      SO3(:,:,irot) = matmul(matmul(br2, rotation(:,:,irot)), br4)
    enddo
    
    write(177,'(A)')'space_rot         tau                        space O3                         spin O3                                  spin SU2'
    do i=1,nsym
      if (time_reversal(i)==1) then
        write(177,'(A,1I4,A)')'#',i,'   without time reversal'
      else
        write(177,'(A,1I4,A)')'#',i,'   with time reversal'
      endif
      write(177,'(3I3,A,1F10.5,A,3F10.5,A,3F10.5,A,4F10.5)')rotation(1,1:3,i),'  ',tau(1,i),'  ',SO3(1,1:3,i),'  ',spin_rotation(1,1:3,i),'  ',spin_SU2(1,1:2,i)
      write(177,'(3I3,A,1F10.5,A,3F10.5,A,3F10.5,A,4F10.5)')rotation(2,1:3,i),'  ',tau(2,i),'  ',SO3(2,1:3,i),'  ',spin_rotation(2,1:3,i),'  ',spin_SU2(2,1:2,i)
      write(177,'(3I3,A,1F10.5,A,3F10.5,A,3F10.5)')rotation(3,1:3,i),'  ',tau(3,i),'  ',SO3(3,1:3,i),'  ',spin_rotation(3,1:3,i)
    enddo


    write(*,'(A)')''
    ! write(*,'(A)')'*******SpinSpaceGroup (SSG) operators********************************************'
    i = Verify(ssg_label,' ')
    if (i /= 0 .and. ssg_label(i:i) /= 'n') then
      write(*,'(A)')'It belongs to Spin Space Group (SSG) ' // trim(adjustl(ssg_label)) // ' .'
    endif
    write(tmp,'(I0)')nsym
    write(*,'(A)')'the list of '//trim(adjustl(tmp))//' SSG operators:    { spin part ||  Ri  |  taui }'
    write(*,'(A)')'   spin part(O3)       Ri     taui   Ri(Cart. Coord.)          Ri(spin-1/2)'
    do i=1,nsym
      if (time_reversal(i)==1) then
        write(*,'(A,1I4,A)')'#',i,'   without time reversal'
      else
        write(*,'(A,1I4,A)')'#',i,'   with time reversal'
      endif
      write(*,'(3F6.3,A,3I2,A,1F7.3,A,3F6.3,A,2F6.3,A,2F6.3,A)')spin_rotation(1,1:3,i),'  ',rotation(1,1:3,i),' ',tau(1,i),'  ',SO3(1,1:3,i),'  (',spin_SU2(1,1,i),') (',spin_SU2(1,2,i),')'
      write(*,'(3F6.3,A,3I2,A,1F7.3,A,3F6.3,A,2F6.3,A,2F6.3,A)')spin_rotation(2,1:3,i),'  ',rotation(2,1:3,i),' ',tau(2,i),'  ',SO3(2,1:3,i),'  (',spin_SU2(2,1,i),') (',spin_SU2(2,2,i),')'
      write(*,'(3F6.3,A,3I2,A,1F7.3,A,3F6.3,A)')spin_rotation(3,1:3,i),'  ',rotation(3,1:3,i),' ',tau(3,i),'  ',SO3(3,1:3,i)
    enddo

  endsubroutine

  subroutine get_ssgrep_through_SSGREP(num_litt_group,rot,tau,spin_rotation,SU2,time_reversal)
  implicit none
  integer,intent(in) :: num_litt_group
  integer,intent(in) :: rot(3,3,num_litt_group)
  real(dp),intent(in) :: tau(3,num_litt_group)
  complex(dp),intent(in) :: SU2(2,2,num_litt_group)
  integer,intent(in) :: time_reversal(num_litt_group)
  real(dp),intent(in) :: spin_rotation(3,3,num_litt_group)
  complex(dp) :: time_rev_mat(2,2)
  
  integer :: i, ios 

  time_rev_mat = 0.0_dp
  time_rev_mat(1,2) = cmplx(-1.0_dp,0.0_dp,dp)
  time_rev_mat(2,1) = cmplx(1.0_dp,0.0_dp,dp)

  open(unit=ssg_file_unit, file='space_rot_k.mat', status='replace', action='write', form='formatted', iostat=ios)
  do i=1,num_litt_group
    write(ssg_file_unit,'(A,I3)') '# ',i
    write(ssg_file_unit,'(3I4)') rot(1,1:3,i)
    write(ssg_file_unit,'(3I4)') rot(2,1:3,i)
    write(ssg_file_unit,'(3I4)') rot(3,1:3,i)
    write(ssg_file_unit,*)
  enddo
  close(ssg_file_unit)

  open(unit=ssg_file_unit, file='spin_rot_k.mat', status='replace', action='write', form='formatted', iostat=ios)
  do i=1,num_litt_group
    write(ssg_file_unit,'(A,I3)') '# ',i
    write(ssg_file_unit,'(3F12.6)') spin_rotation(1,1:3,i)
    write(ssg_file_unit,'(3F12.6)') spin_rotation(2,1:3,i)
    write(ssg_file_unit,'(3F12.6)') spin_rotation(3,1:3,i)
    write(ssg_file_unit,*)
  enddo
  close(ssg_file_unit)

  open(unit=ssg_file_unit, file='spin_SU2_k.mat', status='replace', action='write', form='formatted', iostat=ios)
  do i=1,num_litt_group
    write(ssg_file_unit,'(A,I3)') '# ',i
    write(ssg_file_unit,*) time_reversal(i)
    if(time_reversal(i) == 1) then
      write(ssg_file_unit,'(2F12.6)') SU2(1,1:2,i)
      write(ssg_file_unit,'(2F12.6)') SU2(2,1:2,i)
    else
      write(ssg_file_unit,'(2F12.6)') matmul(SU2(1,1:2,i),time_rev_mat)
      write(ssg_file_unit,'(2F12.6)') matmul(SU2(2,1:2,i),time_rev_mat)
    endif
    write(ssg_file_unit,*)
  enddo
  close(ssg_file_unit)

  open(unit=ssg_file_unit, file='space_tau_k.mat', status='replace', action='write', form='formatted', iostat=ios)
  do i=1,num_litt_group
    write(ssg_file_unit,'(A,I3)') '# ',i
    write(ssg_file_unit,'(3F12.8)') tau(:,i)
    write(ssg_file_unit,*)
  enddo
  close(ssg_file_unit)


  endsubroutine get_ssgrep_through_SSGREP
end module get_ssg_mod
